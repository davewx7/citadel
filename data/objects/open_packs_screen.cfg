{
	id: "open_packs_screen",
	is_strict: true,

	hidden_in_game: true,

	properties: {
		_hidden_objects: { type: "[custom_obj]", default: [] },
		controller: { type: "obj title_controller" },

		_button: { type: "null|obj button_controller" },
		_pack: { type: "null|obj pack" },

		_exhausted_packs: { type: "bool", default: false },

		spawn_button: "def() ->commands
		execute(me,
		[

		remove_object(_button),
		set(_button, null),

		remove_object(_pack),
		set(_pack, null),

		if(num_owned_packs > 0, [

			spawn('button_controller', {
				mid_x: level.dimensions[2]/2,
				mid_y: level.dimensions[3]/2 + lib.gui.py(220),
				text: 'Open Pack',
				button_width: lib.gui.py(120),
				button_height: lib.gui.py(40),
				color_scheme: lib.citadel.library_color_scheme,
				on_click: me.open_pack,
			}, [
				set(_button, child),
			]),

			spawn('pack', {
				mid_x: level.dimensions[2]/2,
				mid_y: level.dimensions[3]/2,
				zorder: 1000,
				scale: lib.gui.dpy(1.0),
			}, [
				set(_pack, child),
			]),

			set(_exhausted_packs, false),

		], [

			spawn('label', 0, 0, {
				mid_x: level.dimensions[2]/2,
				mid_y: level.dimensions[3]/2,
				_text: [q(You don't have any unopened packs)],
				_color: [1,1,1,1],
				_font_size: 24,
			}, [
				add(_widgets, [child]),
			]),

			set(_exhausted_packs, true),
		]),

		])
		",

		_widgets: { type: "[custom_obj]", default: [] },

		create_widgets: "def() ->commands
		[
		map(_cards, remove_object(value)),
		set(_cards, []),

		map(_widgets, remove_object(value)),
		set(_widgets, []),

		spawn('combo_label', 0, 0, {
			x: lib.gui.py(112),
			y: level.dimensions[3] - lib.gui.py(54),
			_text: 'Main Menu',
			_font_size: lib.gui.py(26),
			left_rect: lib.gui.lib.gui.py(-4),
			right_rect: lib.gui.lib.gui.py(2),
			grouping: 'back',
			index: 0,
			click_handler: (def(obj combo_label lb) ->commands return_to_titlescreen()),
		}, [
			add(_widgets, [child]),
		]),

		spawn('pack', {
			mid_x: lib.gui.py(1494),
			mid_y: lib.gui.py(450),
			scale: lib.gui.dpy(0.3),
			zorder: 1000,
		}, [
			add(_widgets, [child]),
		]),

		spawn('label', 0, 0, {
			x2: lib.gui.py(1520),
			y: lib.gui.py(500),
			_text: ['Core set pack: 250 gold'],
			_color: if(controller.client_state.account_info.gold >= 250, [1,1,1,1], [1,0,0,1]),
			_font_size: 16,
		}, [
			add(_widgets, [child]),
		]),

		if(controller.client_state.account_info.gold >= 250,
			spawn('button_controller', {
				mid_x: lib.gui.py(1494),
				mid_y: lib.gui.py(554),
				text: 'Buy',
				_font_size: lib.gui.py(14),
				button_width: lib.gui.py(60),
				button_height: lib.gui.py(20),
				color_scheme: lib.citadel.library_color_scheme,
				on_click: (def()->commands controller.buy_pack('core')),
			}, [
				add(_widgets, [child]),
			])
		),

		spawn_button(),

		]
		",

		open_pack: "def() ->commands
		[
			if(_pack,
				animate(_pack, {
					brightness: 1000,
				}, {
					duration: 20,
				})
			),
			remove_object(_button),
			set(_button, null),
			controller.send_message({
				type: 'open_pack',
				set: 'core',
			}),
		]
		",

		mouseover_card: "def(int n) ->commands
		if(card.cycle > (n+1)*20,
		[
			animate(card, {
				card_size: 3.0,
				mid_x: level.dimensions[2]/2 + int(0.75*lib.gui.py(500)*sin(angle)),
				mid_y: level.dimensions[3]/2 + int(0.75*lib.gui.py(300)*cos(angle)),
			}, {
				duration: 20,
			}),

			set(card.zorder, 801)
		]
		)
		where card = _cards[n]
		where angle = 360.0 - (decimal(n+1)/size(_cards))*360.0
		",

		mouseoff_card: "def(int n) ->commands
		if(card.cycle > (n+1)*20,
		[
			animate(card, {
				card_size: 1.5,
				mid_x: level.dimensions[2]/2 + int(lib.gui.py(500)*sin(angle)),
				mid_y: level.dimensions[3]/2 + int(lib.gui.py(300)*cos(angle)),
			}, {
				duration: 20,
				on_complete: set(card.zorder, 800 - n),
			}),
		]
		)
		where card = _cards[n]
		where angle = 360.0 - (decimal(n+1)/size(_cards))*360.0
		",

		_cards: { type: "[obj card]", default: [] },

		pack_opened: "def(map msg) ->commands
		execute(me,
		[
			if(_pack != null, [
				spawn('fizzle_effect', {
					mid_x: _pack.mid_x,
					mid_y: _pack.mid_y,
					zorder: 1200,
					scale: lib.gui.dpy(0.5),
				}),

				remove_object(_pack),
				set(_pack, null),
			]),

			map(_cards, remove_object(value)),
			set(_cards, []),

			map(reverse(contents), [
				spawn('card', {
					mid_x: level.dimensions[2]/2,
					mid_y: level.dimensions[3]/2,
					zorder: 800 - index,
					card_type: lib.citadel.create_card(value.name),
					in_hand: false,
					allow_drag: false,
					card_size: 1.5,
					show_shadow: true,
					use_absolute_screen_coordinates: true,
					mouse_enter_handler: (def() ->commands mouseover_card(index)),
					mouse_leave_handler: (def() ->commands mouseoff_card(index)),
				}, [
					add(_cards, [child]),
					schedule(index*20,
						animate(child, {
							mid_x: level.dimensions[2]/2 + int(lib.gui.py(500)*sin(angle)),
							mid_y: level.dimensions[3]/2 + int(lib.gui.py(300)*cos(angle)),
						}, {
							duration: 20,
						})
					),
				])
			] where angle = 360.0 - (decimal(index+1)/size(contents))*360.0),

			spawn('button_controller', {
				mid_x: level.dimensions[2]/2,
				mid_y: level.dimensions[3]/2,
				text: 'Continue',
				button_width: lib.gui.py(120),
				button_height: lib.gui.py(40),
				color_scheme: lib.citadel.library_color_scheme,
				on_click: me.finish_pack,
			}, [
				set(_button, child),
			]),

		])
			where set = string<- msg['set']
			where contents = [CardInstance]<- msg['contents']
		",

		finish_pack: "def() ->commands
		create_widgets()
		",

		num_owned_packs: "int :: controller.client_state.account_info.packs['core']",

		return_to_titlescreen: "def() ->commands
		execute(me,
		controller.transition_screen(
		; [
			remove_object(me),
		],
		[enum transition_after_zoom, enum transition_all_zorders, enum transition_no_remove_objects]
		)
		)
		",
	},

	events: {
		create: "[
			[[add(_hidden_objects, [c]), add(c.y, lib.gui.py(10000))] | c <- level.chars, c != me, c != controller, c not in profile_pane_children],
			create_widgets(),
		]
		where profile_pane_children = controller.spawned_descendants(profile_pane)
		where profile_pane = find_or_die(level.chars, value is obj player_profile_pane)",

		process: "[
			if(_exhausted_packs and num_owned_packs > 0, create_widgets()),
		]",

		being_removed: "[
			map(spawned_children, remove_object(value)),
			map(_hidden_objects, add(value.y, -lib.gui.py(10000))),
		]",
	},
}
