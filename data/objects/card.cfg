{
	is_strict: true,
	"animation": [

		{
			"id": "main",
			"image": "card-silver.png",
			"rect": [0,0,145,203],
			scale: 1
		},

		{
			"id": "big",
			"image": "card-silver.png",
			"rect": [0,0,145,203],
			scale: 1
		},

	],
	"id": "card",

	always_active: true,

	vars: {
		_card_type: null,
		player: null,
		controller: null,
		creature: null,
		discarded: false,
		sent_discard_msg: false,
		in_hand: false,
		is_choice: false,
		choice_value: null,
		hand_index: null,
		allow_drag: false,
		drag_pos: null,
		stack_id: null,
		do_not_remove: false, //this object is removing itself so don't remove it otherwise.

		buttons_widget: null,
		keep_label: null,

		render_scale: 2,
		card_size: 1,

		noise_texture: null,

		tooltips: [],
	},

	on_spawned: "[set(vars.controller,
	   if(spawner.type = 'citadel_controller', spawner,
		  find(level.chars, value.type = 'citadel_controller')))
	]",

	on_drag_start: "[
		swallow_mouse_event(),
		if(vars.allow_drag, set(vars.drag_pos, [mouse_x, mouse_y]))
	]",

	on_drag: "[
	  swallow_mouse_event(),
	  if(vars.drag_pos and vars.allow_drag, [
		set(vars.drag_pos, [mouse_x, mouse_y]),
		add(x, dx),
		add(y, dy),
		map(draw_primitives, set(value.points,
		       [[value.points[0][0] + dx, value.points[0][1] + dy]] +
			   value.points[1:])),
	] where dx = mouse_x - vars.drag_pos[0]
	  where dy = mouse_y - vars.drag_pos[1])]",
	on_drag_end: "[swallow_mouse_event(), if(vars.allow_drag, set(vars.drag_pos, null))]",

	on_click: "[
		debug('click card: ' + vars.hand_index),
		swallow_mouse_event(),
		if(vars.is_choice,
	       vars.controller.option_clicked(vars.choice_value),
	[
	 switch(mouse_button,
	  1, vars.controller.card_clicked(me),
	  3, vars.controller.card_right_clicked(me))
	])
	]",

	//mouseover_delay: 20,
	on_mouse_enter: "vars.controller.mouse_enter_card(me)",
	on_mouse_leave: "[
		vars.controller.mouse_leave_card(me),
		if(me.widgets.ability_tooltip, [set(me.widgets.ability_tooltip, null)]),
	]",

	on_mouse_move: "if(vars.in_hand and tip != null, [
		set(me.widgets.child, create_tooltip_widget(tip)),
	],
	if(me.widgets.ability_tooltip, [set(me.widgets.ability_tooltip, null)])
	)
	  where tip = class tooltip_area|null<-
	    find(vars.tooltips, value.point_in_area(xpos, ypos))
	  where xpos = mouse_x - x
	  where ypos = mouse_y - y",

	on_animate_draw: "animate_draw(vars.hand_index*10)",

	properties: {
		clear_widget_tooltips: "def() -> commands set(vars.tooltips, [])",

		add_widget_tooltip: "def(w, int x, int y) -> commands [
			if(w.tooltip != null, add(vars.tooltips, [construct('tooltip_area', { area: map([x + w.x, y + w.y, w.w, w.h], int(value*size_ratio)), tooltip: w.tooltip})])),
			map(w.children, add_widget_tooltip(value, x + w.x, y + w.y)),
		] where size_ratio = card_size/2.0",

		create_tooltip_widget: "def(class tooltip_area tip) widget(me, {
			type: 'dialog',
			id: 'ability_tooltip',
			alpha: 175,
			clear_backround_alpha: 120,
			padding: 0,
			frame_padding: [10,10],
			zorder: 2000,
			cursor:[10, 10],
			x: tip.area[0],
			y: tip.area[1] - 40,
			background_frame: 'empty_window',
			children: [
				{
					type: 'label',
					font: 'RobotoCondensed-Regular',
					alpha: 255,
					size: 16,
					color: 'light_gray',
					text: tip.tooltip,
				}
			],
		})",

		start_fizzle_animation: "def() set(shader.uniform_commands.fizzle_cycle, {value: 1, increment: true})",
		start_resolve_animation: "def() execute(me, [
			schedule(5, [set(shader.uniform_commands.resolve_cycle, {value: 1, increment: true}), set(me.effects[0].enabled, false)]),
			set(me.effects[0].enabled, true),
		])",

		show_halo: "def() if(not effects[0].enabled, [
			add(me.zorder, 5),
			set(me.effects[0].enabled, true),
		])",
		hide_halo: "def() if(effects[0].enabled, [
			add(me.zorder, -5),
			set(me.effects[0].enabled, false),
		])",

		show_keep_button: "def() execute(me, [hide_keep_button(),  spawn('card.keep_button', mid_x-24, int(mid_y+80*card_size), facing, [set(vars.buttons_widget, child)])])",
		hide_keep_button: "def() [execute(me, if(vars.buttons_widget, [vars.buttons_widget.destroy(), set(vars.buttons_widget, null)])), hide_halo()]",

		toggle_keep: "def() if(vars.keep_label, [set(vars.keep_label, null), remove_object(vars.keep_label)], spawn('card.keep_label', mid_x+30, mid_y, facing, [set(vars.keep_label, child)]))",

		marked_as_keep: "vars.keep_label != null",

		render_scale: { get: "vars.render_scale", set: "set(vars.render_scale, value)" },
		card_size: { get: "vars.card_size", set: "set(vars.card_size, value)" },

		destroy: "def() [if(vars.keep_label, remove_object(vars.keep_label)), if(vars.buttons_widget, vars.buttons_widget.destroy()), if(vars.creature, vars.creature.destroy()), set(vars.buttons_widget, null), set(vars.creature, null), remove_object(me)]",
		animate_draw: "def(nstagger) execute(me, [
			set(me.x, -1000), //hack hack! Take it off screen until schedule starts
			set(me.y, -1000),
			map(range(26), schedule(nstagger + value, [
				set(me.x, x + index*(nstagger/10 - 2)),
				set(me.y, y - index*10),
				set(me.alpha, min(255, value*20)),
			] where index = 25 - value)),
		])",

		set_discarded: "def(val) [if(val,
		  execute(me, [set(vars.discarded, true)]),
		  execute(me, [set(vars.discarded, false)])),
		  debug('SET DISCARDED ' + val),
		  set(me.red, col),
		  set(me.green, col),
		  set(me.blue, col),
		] where col = if(val, 32, 255)
		  ",


		//card_type property which can be a creature OR an activated_ability.
		card_type: {
		  get: "vars._card_type", set: "execute(me, [
			if(creature_obj, [
					set(child.vars.controller, vars.controller),
					set(vars.creature, child), 
					set(child.zorder, zorder+5), 
					set(child.creature_object, creature_obj),
				]
				where child = object('creature', if(creature_obj.sprite_sheet = 'creature_large', 180, 80)*render_scale, 52*render_scale, 1)
				) where creature_obj = if(value._class != 'activated_ability', value.creature_object),
				set(vars._card_type, value), 
				fire_event('init')
			  ]) asserting value != null
			     asserting vars.controller"
		},
	},

	on_toggle_keep: "toggle_keep()",

	on_init: "[
		if(marked_as_keep, [fire_event('toggle_keep'), fire_event('toggle_keep')]),
		[
			set(render_obj.draw_primitives,
			  map(card_type.runes, {type: 'arrow', texture: 'ink.png', texture_scale: 0.2, arrow_head_width: 0.25, fade_in_length: 10, width_base: 10.0, width_head: 10.0, arrow_head_length: 40} + value + {points: if(size(p) = 2, p + [p[1]], p) where p = map(value.points, [value[0]*2 + 50, value[1]*1.4 + 70])})),
			set(render_obj.parent, me),
			fire_event(render_obj, 'init'),
			fire_event('render', {render_obj: render_obj}),
		]
		where render_obj = object_playable('card.render', img_w/2, img_h/2, 1),
	]",

	on_render: "[
		set(animation, {
			id: 'fbo',
			image: 'fbo',
			fbo: texture([render_obj], [0,0,145*render_scale,203*render_scale]),
			x: 0,
			y: 0,
			w: 145*render_scale,
			h: 203*render_scale,
			scale: card_size/2.0,
			frames: 1,
			duration: -1
		}),

		clear_widget_tooltips(),
		map(render_obj.widgets.children, add_widget_tooltip(value, 0, 0)),
	]",

	object_type: [
	{
		id: "keep_button",
		hidden_in_game: true,

		tmp: {
			parent: null
		},

		animation: {
			id: "normal",
			image: "effects/particles.png",
			x: 86, y: 73, w: 28, h: 28,
			frames: 1,
		},

		properties: {
			destroy: "def() execute(me, [set_widgets([]), remove_object(me)])",
		},

		on_spawned: "[set(zorder, spawner.zorder+10), set(tmp.parent, spawner)]",

		on_create: "set_widgets([{
			type: 'grid',
			columns: 2,
			children: [
			{
				type: 'button',
				resolution: 'normal',
				label: {type: 'label', text: 'Keep', font: 'RobotoCondensed-Bold', color: 'antique_white'},
				on_click: def() [if(tmp.parent.vars.discarded, tmp.parent.vars.controller.card_clicked(tmp.parent)), tmp.parent.toggle_keep()],
			},
			{
				type: 'button',
				resolution: 'normal',
				label: {type: 'label', text: 'Discard', font: 'RobotoCondensed-Bold', color: 'antique_white'},
				on_click: def() tmp.parent.vars.controller.card_right_clicked(tmp.parent),
			}
			]
		}])",
	},

	{
		id: "keep_label",
		hidden_in_game: true,

		tmp: {
			state: null
		},

		animation: {
			id: "normal",
			image: "effects/particles.png",
			x: 86, y: 73, w: 28, h: 28,
			frames: 1,
		},

		on_spawned: "set(zorder, spawner.zorder+100)",

		on_create: "set_widgets([{
			type: 'label',
			text: 'Keep',
			font: 'RobotoCondensed-Bold',
			align_h: 'center',
			color: 'antique_white',
			size: 42
		}])",
	},

	{
		id: "render",
		is_strict: true,
		is_human: true,

		animation: [{
			"id": "main",
			"image": "card-silver.png",
			"rect": [0,0,145,203],
			scale: 1
		},
		{
			"id": "big",
			"image": "card-silver.png",
			"rect": [0,0,145,203],
			scale: 2
		}
		],

		properties: {
			card_type: "parent.card_type",
			render_scale: "parent.render_scale",
		},

		tmp: {
			left_color: [1.0,1.0,1.0,1.0],
			right_color: [1.0,1.0,1.0,1.0],
		},

		on_process: "console_output_to_screen(false)",

		on_init: "[
		[
			if(parent.render_scale = 2, set(animation, 'big')),
			set(tmp.left_color, school_colors[0]),
			set(tmp.right_color, school_colors[size(school_colors)-1]),
		] where school_colors = map(school, [value.red, value.green, value.blue, 1.0])
		  where school = map(card_type.school, get_document('data/schools.cfg')[value]),
		set_widgets([{
			type: 'rich_text_label',
			width: 120*render_scale,
			x: 14*render_scale,
			y: 126*render_scale,
			claim_mouse_events: false,
			children: card_type.rules_text_widgets(render_scale) asserting card_type.rules_text_widgets | [card_type.rules_text_widgets, card_type.type, card_type],
			zorder: 11
		},
	  {
		 type: 'rich_text_label',
		 width: 132*render_scale,
		 align: 'right',
		 x: 1*render_scale,
		 y: 6*render_scale,
	     zorder: 10,
		 children:
	  map(card_type.school, school,
	   map(range(card_type.loyalty_cost),
	       singleton('gui_widgets').resource_image(school, 9*render_scale) + {alpha: if(parent.vars.player and parent.vars.player.resource_level[school] > index, 96, 255)})) +
		   [
		   	  {
				  type: 'label',
				  color: 'black',
				  size: render_scale*14,
				  font: card_type.card_font,
				  text: '' + card_type.cost,
			  }
		   ]
		
	  }] +

	  if(parent.vars.creature, [{
		type: 'object',
		object: parent.vars.creature,
		claim_mouse_events: false,
	  }], [])
	  
	  ),

	 if(render_scale in [1,2],

	  [textv(me, {text: card_type.name, align: 'left', color: 'black', size: if(render_scale = 1, 12, 18), rect: [12*render_scale, 9*render_scale, 120*render_scale, 20*render_scale], font:'RobotoCondensed-Light'},

		   if(creature_object != null,
				   {text: if(card_type.hero, 'Hero -- ', if(creature_object.is_construct, 'Construct -- ', 'Creature -- ')) + fold(card_type.creature_object.tags, a + ' ' + b, ''), align: 'left', color: 'black', size: if(render_scale = 1, 10, 14), rect: [15*render_scale, 115*render_scale, 180*render_scale, 130*render_scale], font:'RobotoCondensed-LightItalic'},
				   {text: if(card_type._class = 'card', 'Spell', 'Ability') + if(card_type.is_response, ' -- Response', ''), align: 'left', color: 'black', size: if(render_scale = 1, 10, 14), rect: [15*render_scale, 115*render_scale, 180*render_scale, 130*render_scale], font:'RobotoCondensed-LightItalic'},
				   ),
			if(creature_object != null,
		   {text: card_type.creature_object.attack + '/' + card_type.creature_object.life, align: 'left', color: 'black', size: if(render_scale = 1, 14, 18), rect: [110*render_scale, 178*render_scale, 40*render_scale, 30*render_scale], font:'RobotoCondensed-LightItalic'}))],
	
	  //otherwise render_scale = 2
	  [textv(me, {text: card_type.name, align: 'left', color: 'black', size: 18, rect: [-48, -80, 300, 20], font:'RobotoCondensed-Regular'},
		   if(creature_object != null,
				   {text: if(card_type.hero, 'Hero -- ', if(creature_object.is_construct, 'Construct -- ', 'Creature -- ')) + fold(card_type.creature_object.tags, a + ' ' + b, ''), align: 'left', color: 'black', size: 14, rect: [-46, 128, 300, 180], font:'RobotoCondensed-Italic'},
				   {text: 'Spell' + if(card_type.is_response, ' -- Response', ''), align: 'left', color: 'black', size: 14, rect: [-46, 130, 300, 180], font:'RobotoCondensed-Italic'},
				   ),
			if(creature_object != null,
		   {text: card_type.creature_object.attack + '/' + card_type.creature_object.life, align: 'left', color: 'black', size: 18, rect: [156, 262, 40, 30], font:'RobotoCondensed-Italic'}))]
	),
	] where creature_object = if(card_type._class != 'activated_ability', card_type.creature_object)",

		shader: {
			name: "card_render",
			create: "[
				set(uniform_commands.u_left_color, parent.tmp.left_color),
				set(uniform_commands.u_right_color, parent.tmp.right_color),
			]",

			draw: "[
				bind_texture(current_texture, 0),
			]",

			vertex: "
				uniform mat4 u_mvp_matrix;
				attribute vec4 a_position;
				attribute vec2 a_texcoord;
				varying vec2 v_texcoord;
				varying vec2 v_cardcoord;
				void main()
				{
					v_texcoord = a_texcoord;
					gl_Position = u_mvp_matrix * a_position;
					v_cardcoord = vec2(0.0, 0.0);
					if(a_texcoord[0] > 0.5) {
						v_cardcoord[0] = 1.0;
					}

					if(a_texcoord[1] > 0.5) {
						v_cardcoord[1] = 1.0;
					}
				}
			",

			fragment: "
				uniform sampler2D u_tex_map;
				uniform vec4 u_left_color;
				uniform vec4 u_right_color;
				varying vec2 v_texcoord;
				varying vec2 v_cardcoord;
				void main()
				{
					gl_FragColor = texture2D(u_tex_map, v_texcoord);

					vec4 color = u_left_color;
					if(v_cardcoord[0] > 0.4) {
						if(v_cardcoord[0] > 0.6) {
							color = u_right_color;
						} else {
							float ratio = (v_cardcoord[0] - 0.4)*5.0;
							color = u_right_color*ratio + u_left_color*(1.0-ratio);
						}
					}

					gl_FragColor = gl_FragColor * (color/2.0 + vec4(0.5,0.5,0.5,0.5));
				}
			",

			attributes: {
				vertex: "a_position",
				texcoord: "a_texcoord",
			},

			uniforms: {
				mvp_matrix: "u_mvp_matrix",
			},
		},
	},
	],

	shader: {
		program: "card",
		create: "[
			set(uniform_commands.u_tex_map, 0),
			set(uniform_commands.u_tex_noise, 1),
			set(uniform_commands.fizzle_cycle, 0),
			set(uniform_commands.resolve_cycle, 0),
			set(parent.vars.noise_texture, load_texture('images/noise.png')),
		]",

		draw: "[
			set(uniform_commands.u_color, color),
			bind_texture(current_texture, 0),
			bind_texture(parent.vars.noise_texture.id, 1),
		]",
	},

	effects: [{
		name: "card_halo",
		create: "[
			set(enabled, false),
			set(attribute_commands.a_texcoord, float_array([
			   0.0, 0.0,
			   1.0, 0.0,
			   0.0, 1.0,
			   1.0, 1.0,
			], 2)),
		]",

		vertex: "
			uniform mat4 u_mvp_matrix;
			uniform vec4 u_parent_pos;
			attribute vec2 a_position;
			attribute vec2 a_texcoord;
			varying vec2 v_texcoord;
			void main()
			{
				v_texcoord = a_texcoord;
				vec4 v = vec4(a_position.x, a_position.y, 0.0, 1.0);
				gl_Position = u_mvp_matrix * v;
			}",

		fragment: "
			varying vec2 v_texcoord;
			void main()
			{
				gl_FragColor[0] = 1.0;
				gl_FragColor[1] = 1.0;
				gl_FragColor[2] = 1.0;
				gl_FragColor[3] = 1.0;

				float edge = 0.1;

				if(v_texcoord[0] < edge) {
					float ratio = v_texcoord[0]/edge;
					gl_FragColor[3] = gl_FragColor[3]*ratio;
				} else if(v_texcoord[0] > 1.0 - edge) {
					float ratio = (1.0 - v_texcoord[0])/edge;
					gl_FragColor[3] = gl_FragColor[3]*ratio;
				}

				if(v_texcoord[1] < edge) {
					float ratio = v_texcoord[1]/edge;
					gl_FragColor[3] = gl_FragColor[3]*ratio;
				} else if(v_texcoord[1] > 1.0 - edge) {
					float ratio = (1.0 - v_texcoord[1])/edge;
					gl_FragColor[3] = gl_FragColor[3]*ratio;
				}
			}
		",

		"uniforms": {
			"mvp_matrix": "u_mvp_matrix",
		},
		
		draw: "[
			set(attribute_commands.a_position, float_array([
			   parent.x-border_size, parent.y-border_size,
			   parent.x2+border_size, parent.y-border_size,
			   parent.x-border_size, parent.y2+border_size,
			   parent.x2+border_size, parent.y2+border_size,
			], 2)) where border_size = parent.img_w/10.0,
			draw_arrays('triangle_strip', 0, 4),
		]",
	}],
}
